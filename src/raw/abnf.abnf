; Equivalent to the syntax defined in:
; https://www.rfc-editor.org/rfc/rfc5234#section-4
; with several QoL improvement.

rulelist       =  expression *(crlf expression)

expression     =  rule / *wsp [comment]

rule           =  rulename [empty] defined-as [empty] alternation *wsp [comment]
                      ; continues if next line starts
                      ;  with white space

rulename       =  alpha *(alpha / dec / "-")

defined-as     =  "=/" / "="
                      ; basic rules definition and
                      ;  incremental alternatives

alternation    =  concatenation *([empty] "/" [empty] concatenation)

concatenation  =  repetition *(empty repetition)

repetition     =  option /
                  [repeat] (
                    rulename /
                    group /
                    char-val /
                    bin-val /
                    dec-val /
                    hex-val)

repeat         =  1*dec / *dec "*" *dec

option         =  "[" [empty] alternation [empty] "]"

group          =  "(" [empty] alternation [empty] ")"

char-val       =  %x22 *(%x20-21 / %x23-7E) %x22
                      ; quoted string of SP and VCHAR
                      ;  without quote

bin-val        =  "%b" 1*bit [ 1*("." 1*bit) / ("-" 1*bit) ]
                      ; series of concatenated bit values
                      ;  or single ONEOF range

dec-val        =  "%d" 1*dec [ 1*("." 1*dec) / ("-" 1*dec) ]

hex-val        =  "%x" 1*hex [ 1*("." 1*hex) / ("-" 1*hex) ]

empty-line     =  *wsp [comment] crlf

empty          =  *empty-line 1*wsp

comment        =  ";" *(wsp / %x21-7E)

alpha          =  %x41-5A / %x61-7A   ; A-Z / a-z

wsp            =  %x20 / %x09
                      ; white space

crlf           =  %x0D.0A / %x0A
                      ; Internet standard newline

bit            =  "0" / "1"

dec            =  %x30-39
                      ; 0-9

hex            =  %x30-39 / %x41-46 / %x61-66